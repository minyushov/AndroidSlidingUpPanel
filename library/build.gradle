apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
  compileSdkVersion androidCompileSdkVersion

  defaultConfig {
    minSdkVersion androidMinSdkVersion
    targetSdkVersion androidTargetSdkVersion
  }

  compileOptions {
    sourceCompatibility javaVersion
    targetCompatibility javaVersion
  }
}

dependencies {
  implementation "androidx.recyclerview:recyclerview:1.2.0-beta01"
}

tasks.register("sourcesJar", Jar) {
  archiveClassifier = "sources"
  from(android.sourceSets.main.java.srcDirs)
}

afterEvaluate {
  def properties = new Properties()
  def file = rootProject.file("local.properties")
  if (file.isFile()) {
    file.withInputStream { stream ->
      properties.load(stream)
    }
  }

  publishing {
    publications {
      maven(MavenPublication) {
        groupId "io.github.minyushov"
        artifactId = "sliding-panel"
        version = "3.7.0"

        from(components.findByName("release"))
        artifact(tasks.named("sourcesJar"))

        pom {
          name = artifactId
          description = "This library provides a simple way to add a draggable sliding up panel"
          url = "https://github.com/minyushov/sliding-panel"
          developers {
            developer {
              id = "minyushov"
              name = "Semyon Minyushov"
              email = "minyushov@gmail.com"
            }
          }
          licenses {
            license {
              name = "Apache-2.0"
              url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          scm {
            connection = "https://github.com/minyushov/sliding-panel.git"
            developerConnection = "https://github.com/minyushov/sliding-panel.git"
            url = "https://github.com/minyushov/sliding-panel/tree/master"
          }
        }
      }
    }
    repositories {
      maven {
        name "sonatype"
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        credentials {
          username properties["SONATYPE_USER"]
          password properties["SONATYPE_PASSWORD"]
        }
      }
    }
  }

  signing {
    useInMemoryPgpKeys(
      properties["GPG_KEY_ID"],
      properties["GPG_KEY"],
      properties["GPG_PASSWORD"]
    )
    sign(publishing.publications)
  }
}